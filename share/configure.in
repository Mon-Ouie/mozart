dnl -*- sh -*-
dnl
dnl  Authors:
dnl    Michael Mehl (mehl@dfki.de)
dnl
dnl  Contributors:
dnl    optional, Contributor's name (Contributor's email address)
dnl
dnl  Copyright:
dnl    Michael Mehl (1997)
dnl
dnl  Last change:
dnl    $Date$ by $Author$
dnl    $Revision$
dnl
dnl  This file is part of Mozart, an implementation
dnl  of Oz 3:
dnl     http://mozart.ps.uni-sb.de
dnl
dnl  See the file "LICENSE" or
dnl     http://mozart.ps.uni-sb.de/LICENSE.html
dnl  for information on usage and redistribution
dnl  of this file, and for a DISCLAIMER OF ALL
dnl  WARRANTIES.
dnl

dnl Process this file with autoconf to produce a configure script.

dnl *****************************************************************
dnl INITIALIZE
dnl *****************************************************************

dnl This will check that we are in the Oz directory and initiate
AC_INIT(lib)

OZ_INIT

dnl *****************************************************************
dnl locations
dnl *****************************************************************

srcdira=`(cd $srcdir; pwd)`
AC_SUBST(srcdira)

dnl *****************************************************************
dnl windows cross compilation
dnl *****************************************************************

case "$target" in
   NONE)
        MSWIN=no
   ;;
   i386-mingw32)
        AC_MSG_RESULT(cross compiling for windows)

        CXX=${target}-gcc
        CC=${CXX}
        platform="win32-i486"
        MSWIN=yes

        AC_SUBST(CC)
        AC_SUBST(CXX)
   ;;

   *)
        AC_MSG_ERROR("can only cross-compile for target i386-mingw32 not for $target")
    ;;
esac

AC_SUBST(MSWIN)

dnl *****************************************************************
dnl Checks for programs.
dnl *****************************************************************

AC_PROG_LN_S

AC_CHECK_PROGS(M4, gm4 m4, m4)
AC_CHECK_PROGS(PERL, perl, perl)
AC_CHECK_PROGS(GZIP, gzip, gzip)
AC_CHECK_PROGS(THEEMACS, emacs xemacs, emacs)

OZ_PATH_PROG(OZPLATFORM, ozplatform)
OZ_PATH_PROG(OZC, ozc,  OZC=`pwd`/lib/ozc)
OZ_PATH_PROG(OZL, ozl, OZL=`pwd`/lib/ozl)
OZ_PATH_PROG(OZENGINE, ozengine, OZENGINE=$srcdira/lib/oze.sh)

AC_SUBST(OZL)

: ${oz_picklecompression=9}

AC_MSG_CHECKING(for --enable-picklecompression)
AC_ARG_ENABLE(picklecompression,
    [--enable-picklecompression=0..9,no
        choose pickle compression level (0 is uncompressed, default: $oz_picklecompression)])

case $enable_picklecompression in
        0|1|2|3|4|5|6|7|8|9)
            oz_picklecompression=${enable_picklecompression}
            ;;
        no)
            oz_picklecompression=0
            ;;
esac

AC_MSG_RESULT($oz_picklecompression)

AC_SUBST(oz_picklecompression)

dnl *****************************************************************
dnl FINALIZE
dnl *****************************************************************

AC_OUTPUT(lib/Makefile lib/support/DefaultURL.oz \
    tools/Makefile tools/ProductionTemplates.oz \
    demo/Makefile test/Makefile \
    examples/Makefile elisp/Makefile elisp/oz.el \
    bin/Makefile bin/ozc.bat \
    Makefile)


errprint(ifdef([OZ_INIT],
 [autoconf succeeded
],
 [autoconf failed: use 'autoconf -l ..' to include ../aclocal.m4.
]))
