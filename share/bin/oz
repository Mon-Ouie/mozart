#!/bin/csh -f
# Script that configures the environment to
# start lemacs for a standalone version of Oz
# MUST reside in <oz dist dir>/bin



# where Oz resides:

set howcalled=$0

if (! $?OZHOME) then
   set whereami = `pwd`
   cd $howcalled:h/..
   setenv OZHOME `pwd`
   cd $whereami
   echo "Setting Oz installation directory to: $OZHOME"
endif


# Where ispgm sits:
if (! $?ISPGM) setenv ISPGM $OZHOME/bin/ispgm


# where Oz searches for files:
if (! $?OZPATH ) setenv OZPATH .:${OZHOME}:${OZHOME}/lib:${OZHOME}/demo


# increment path
setenv PATH ${PATH}:$OZHOME/bin


# where is lucid emacs?
if (! $?LEMACS) then
  if ( -x $OZHOME/lemacs/lemacs ) then
     set LEMACS=$OZHOME/lemacs/lemacs
  else if ( -x /usr/share/dfki/gnu/bin/lemacs ) then
     set LEMACS=/usr/share/dfki/gnu/bin/lemacs
  else
     set LEMACS=lemacs
  endif
endif


# load fonts
if ( -d $OZHOME/demo/fonts ) xset +fp $OZHOME/demo/fonts


# want to use an older version?
if ($?OZVERSION) then
  setenv PATH ${OZVERSION}:$PATH
  setenv OZPATH ${OZVERSION}:$OZPATH
endif


set cmd=`basename $howcalled`

if ($cmd == "oz+") then
  exec $LEMACS -l $OZHOME/elisp/ozstartup.el
endif



#setting line editor
set ILE=ile
if ($TERM == emacs) set ILE=""  # running under emacs


# set echo

if ($cmd == "oz.compiler") then
    if ($TERM == "emacs") then
       exec oz.compiler.bin +E `echo $* |  sed -e 's/-/+/g'`
    else
       exec xterm -name "OzCompiler" \
            -T "Oz Compiler Input Window" \
            -e $ILE oz.compiler.bin `echo $* |  sed -e 's/-/+/g'`
    endif
endif


if ($cmd == "oz.machine") then
   if ($TERM == "emacs") then
     exec oz.machine.bin -E $*
   else
     (xterm -title 'Oz Machine' -e oz.machine.bin $* &) >& /dev/null
   endif
  exit 0
endif

exec $ILE oz.compiler.bin $*
