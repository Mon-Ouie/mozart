dnl -*- sh -*-
dnl
dnl  Authors:
dnl    Denys Duchier (duchier@ps.uni-sb.de)
dnl
dnl  Copyright:
dnl    Denys Duchier (1998)
dnl
dnl  Last change:
dnl    $Date$ by $Author$
dnl    $Revision$
dnl
dnl  This file is part of Mozart, an implementation
dnl  of Oz 3:
dnl     http://www.mozart-oz.org
dnl
dnl  See the file "LICENSE" or
dnl     http://www.mozart-oz.org/LICENSE.html
dnl  for information on usage and redistribution
dnl  of this file, and for a DISCLAIMER OF ALL
dnl  WARRANTIES.
dnl

dnl Process this file with autoconf to produce a configure script.

AC_INIT(ozdoc.dtd)
OZ_INIT
OZ_PATH_PROG(OZENGINE,ozengine)
OZ_EMACS
OZ_EMACS_OPTIONS

# PLATFORM is defined by OZ_INIT
: ${platform=$PLATFORM}

case $platform in
    win32*)
        EXE_EXT='.exe'
        ;;
    *)
        EXE_EXT=''
        ;;
esac

OZ_PATH_PROG(OZDOC,ozdoc,OZDOC=$BUILDTOP/doc/utilities/ozdoc$EXE_EXT)

AC_SUBST(OZENGINE)
AC_SUBST(OZDOC)

AC_MSG_CHECKING(for --with-java-bin)
AC_ARG_WITH(java-bin,
  [--with-java-bin='DIR'
       directory where java executables/wrappers reside],
  oz_java_bin=${with_java_bin})
: ${oz_java_bin=}
AC_MSG_RESULT($oz_java_bin)
if test -n "${oz_java_bin}" ;
then
  if expr "${oz_java_bin}" : '.*/$' ;
  then
    # really do nothing portably
    oz_tmp=
  else
    oz_java_bin=${oz_java_bin}/
  fi
fi
JAVA_BIN=${oz_java_bin}
AC_SUBST(JAVA_BIN)

oz_path_save=$PATH
OZ_PATH_PROG(JAVA,java)
if test -n "$JAVA"; then
  OZ_PATH_PROG(JAVAC,javac)
  OZ_PATH_PROG(JAR,jar)
  AC_MSG_CHECKING(for class path option)
  case `$JAVA -version 2>&1` in
  Kaffe*) JAVACP=-addclasspath;;
  *) JAVACP=-classpath;;
  esac
  AC_MSG_RESULT($JAVACP)
  AC_MSG_CHECKING(for --with-java-threads)
  AC_ARG_WITH(java-threads,
    [--with-java-threads='OPTION'
         option for java threads support selection (e.g. -green) (default=)],
    oz_java_threads=${with_java_threads})
  : ${oz_java_threads=}
  AC_MSG_RESULT($oz_java_threads)
  JAVA_THREADS=${oz_java_threads}
fi
PATH=$oz_path_save
AC_SUBST(JAVA)
AC_SUBST(JAVAC)
AC_SUBST(JAR)
AC_SUBST(JAVACP)
AC_SUBST(JAVA_THREADS)

AC_MSG_CHECKING(for --with-documents)
AC_ARG_WITH(documents,
  [--with-documents='DIR1 ... DIRN' (or all)
       document subdirectories (default=none)],
  oz_doc_dirs=${with_documents})
: ${oz_doc_dirs=}
AC_MSG_RESULT($oz_doc_dirs)

if test "${oz_doc_dirs}" = "all"; then
  oz_doc_dirs="apptut base bugs compiler contrib demo foreign gump op opi ozcar profiler ozdoc system tools wp fdt fst cpitut cpiref install tutorial overview panel explorer inspector browser notation dstutorial changes tcltk howtoread faq limitations macro loop dpanel ds_white_paper releasing"
fi

DOC_DIRS=$oz_doc_dirs
AC_SUBST(DOC_DIRS)

AC_MSG_CHECKING(for --enable-chm)
AC_ARG_ENABLE(chm,
  [--enable-chm
       construct CHM table of contents files (default=no)],
  oz_enable_chm=$enable_chm,
  oz_enable_chm=no)
AC_MSG_RESULT($oz_enable_chm)

if test "${oz_enable_chm}" != no
then
   SUBDIRS="utilities ${oz_doc_dirs} chm"
else
   SUBDIRS="utilities ${oz_doc_dirs}"
fi

AC_SUBST(SUBDIRS)

AC_MSG_CHECKING(for --with-ozhome)
AC_ARG_WITH(ozhome,
  [--with-ozhome=DIR  real home in case of fake root build],
  oz_real_ozhome="$with_ozhome")
: ${oz_real_ozhome=no}
AC_MSG_RESULT($oz_real_ozhome)
if test "${oz_real_ozhome}" = no; then
    if test "${prefix}" = NONE; then
        REAL_OZHOME="$ac_default_prefix"
    else
        REAL_OZHOME="$prefix"
    fi
else
    REAL_OZHOME="$oz_real_ozhome"
fi
AC_SUBST(REAL_OZHOME)

AC_CONFIG_SUBDIRS(utilities chm)

AC_OUTPUT(Makefile Makefile.common Makefile.vars catalog bootcatalog base/Makefile bugs/Makefile foreign/Makefile apptut/Makefile wp/Makefile system/Makefile compiler/Makefile opi/Makefile tools/Makefile ozcar/Makefile profiler/Makefile gump/Makefile ozdoc/Makefile contrib/Makefile demo/Makefile op/Makefile fdt/Makefile fst/Makefile cpitut/Makefile cpiref/Makefile install/Makefile tutorial/Makefile overview/Makefile notation/Makefile browser/Makefile panel/Makefile explorer/Makefile inspector/Makefile dstutorial/Makefile changes/Makefile tcltk/Makefile howtoread/Makefile faq/Makefile limitations/Makefile xsl/Makefile xsl/ozdoc-info.sty:xsl/ozdoc-info.in macro/Makefile loop/Makefile dpanel/Makefile ds_white_paper/Makefile releasing/Makefile)

errprint(ifdef([OZ_INIT],
 [autoconf succeeded
],
 [autoconf failed: use 'autoconf -l ..' to include ../aclocal.m4.
]))
