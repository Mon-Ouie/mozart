dnl
dnl Author:
dnl   Leif Kornstaedt <kornstae@ps.uni-sb.de>
dnl
dnl Copyright:
dnl   Leif Kornstaedt, 1999
dnl
dnl Last change:
dnl   $Date$ by $Author$
dnl   $Revision$
dnl
dnl This file is part of Mozart, an implementation of Oz 3:
dnl    http://www.mozart-oz.org
dnl
dnl See the file "LICENSE" or
dnl    http://www.mozart-oz.org/LICENSE.html
dnl for information on usage and redistribution
dnl of this file, and for a DISCLAIMER OF ALL
dnl WARRANTIES.
dnl

define(instructionsUnneededForNewCompiler,`divert(-1)')
define(readArg,$1)
define(writeArg,$1)

define(instruction,
	`ifelse(numOfRegs($2,$3,$4),1,oneReg($@),
		numOfRegs($2,$3,$4),2,twoRegs($@),
		noReg($@))')

define(arglist,
	`ifelse($#,1,nil,[$2 $3 $4 $5 $6 $7])')

changequote([,])
define(noReg,['$1': none(arglist($@))])
define(oneReg,['$1X': one(arglist($@) $1 x)
'$1Y': one(arglist($@) $1 y)
'$1G': one(arglist($@) $1 g)])
define(twoRegs,['$1XX': two(arglist($@) $1 x x)
'$1XY': two(arglist($@) $1 x y)
'$1XG': two(arglist($@) $1 x g)
'$1YX': two(arglist($@) $1 y x)
'$1YY': two(arglist($@) $1 y y)
'$1YG': two(arglist($@) $1 y g)
'$1GX': two(arglist($@) $1 g x)
'$1GY': two(arglist($@) $1 g y)
'$1GG': two(arglist($@) $1 g g)])
changequote(`,')

define(Register,registerArg)
define(XRegisterIndex,xRegisterIndexArg)
define(YRegisterIndex,yRegisterIndexArg)
define(Arity,integerArg)
define(Count,integerArg)
define(ArityAndIsTail,integerArg)
define(Dummy,integerArg)
define(Number,valueArg)
define(Literal,valueArg)
define(Feature,valueArg)
define(Constant,valueArg)
define(Builtinname,builtinnameArg)
define(Label,labelArg)
define(PredicateRef,predicateRefArg)
define(PredId,predIdArg)
define(HashTableRef,hashTableArg)
define(RecordArity,recordArityArg)
define(GenCallInfo,genCallInfoArg)
define(GRegRef,gRegRefArg)
define(Location,locationArg)
define(Cache,)

InstrArgs =
instrArgs(include(EMUDIR/instrDefs.m4))
